# Maintainer: D. Can Celasun <can[at]dcc[dot]im>

pkgname=visual-studio-code-bin
_pkgname=visual-studio-code
pkgver=1.107.1
pkgrel=1
pkgdesc="Visual Studio Code (vscode): Editor for building and debugging modern web and cloud applications (official binary version)"
arch=('x86_64')
url="https://code.visualstudio.com/"
license=('custom: commercial')
provides=('code' 'vscode')
conflicts=('code')
# lsof: needed for terminal splitting, see https://github.com/Microsoft/vscode/issues/62991
# xdg-utils: needed for opening web links with xdg-open
depends=(
    'alsa-lib'
    'gcc-libs'
    'glibc'
    'gnupg'
    'gtk3'
    'libxkbfile'
    'libsecret'
    'libnotify'
    'libxss'
    'lsof'
    'nss'
    'shared-mime-info'
    'xdg-utils'
)
# icu69: https://github.com/MicrosoftDocs/live-share/issues/4650
optdepends=(
    'glib2: Needed for move to trash functionality'
    'libdbusmenu-glib: Needed for KDE global menu'
    'org.freedesktop.secrets: Needed for settings sync'
    'icu69: Needed for live share'
)
source=(
    "code_x64_${pkgver}.tar.gz::https://update.code.visualstudio.com/${pkgver}/linux-x64/stable"
    "code-${pkgver}.desktop.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code.desktop"
    "code-${pkgver}-url-handler.desktop.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code-url-handler.desktop"
    "code-${pkgver}-workspace.xml.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code-workspace.xml"
    "${_pkgname}-bin.sh"
)
sha256sums=('a9a19e20dd09c61ec1af7d67d9dec2455004d0fbd35120fe1d24588c123f9474'
            '2f1782b30c4e040efff655fd9cf477930c5a0c81ddae27749b0cbb922c1d248e'
            'c361efa7e02fcad759ed80d2fbab67877f33219b981578af6fffaf18aeb12d9b'
            '3af748dd6578a1775e8eb7248ba397b7e11840df2ea6ee234ff76fee3dc306cf'
            'ac61eac5484b8bec249d06ea827167dfe2ff50c99ac5bf230e7a4ee892cc0978')
# Upstream has signature verification for extensions and stripping breaks it
# See https://github.com/microsoft/vscode/issues/223455#issuecomment-2610001754
options=(!strip !emptydirs)

_set_meta_info() {
    sed 's/@@NAME_LONG@@/Visual Studio Code/g' "$1" |\
    sed 's/@@NAME_SHORT@@/Code/g' |\
    sed 's/@@NAME@@/code/g' |\
    sed 's#@@EXEC@@#/usr/bin/code#g' |\
    sed 's/@@ICON@@/visual-studio-code/g' |\
    sed 's/@@URLPROTOCOL@@/vscode/g'
}

prepare() {
    _set_meta_info "${srcdir}/code-${pkgver}.desktop.in" > "${srcdir}/code.desktop"
    _set_meta_info "${srcdir}/code-${pkgver}-url-handler.desktop.in" > "${srcdir}/code-url-handler.desktop"
    _set_meta_info "${srcdir}/code-${pkgver}-workspace.xml.in" > "${srcdir}/code-workspace.xml"
}

package() {
    install -dv "${pkgdir}/usr/lib/${_pkgname}"
    install -dv "${pkgdir}/usr/bin"
    install -dv "${pkgdir}/usr/share/"{applications,pixmaps,mime/packages,licenses/${_pkgname}}

    install -Dvm644 "${srcdir}/VSCode-linux-x64/resources/app/LICENSE.rtf" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.rtf"
    install -Dvm644 "${srcdir}/VSCode-linux-x64/resources/app/resources/linux/code.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
    install -Dvm644 "${srcdir}/code.desktop" "${pkgdir}/usr/share/applications/code.desktop"
    install -Dvm644 "${srcdir}/code-url-handler.desktop" "${pkgdir}/usr/share/applications/code-url-handler.desktop"
    install -Dvm644 "${srcdir}/code-workspace.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}-workspace.xml"
    install -Dvm644 "${srcdir}/VSCode-linux-x64/resources/completions/bash/code" "${pkgdir}/usr/share/bash-completion/completions/code"
    install -Dvm644 "${srcdir}/VSCode-linux-x64/resources/completions/zsh/_code" "${pkgdir}/usr/share/zsh/site-functions/_code"
    install -Dvm755 "${srcdir}/${_pkgname}-bin.sh" "${pkgdir}/usr/bin/code"
    cp -r "${srcdir}/VSCode-linux-x64/"* "${pkgdir}/usr/lib/${_pkgname}"
}
