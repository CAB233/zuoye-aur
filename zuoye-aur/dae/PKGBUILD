# Maintainer: cubercsl <2014cais01 at gmail dot com>
# Maintainer: Integral <integral at member dot fsf dot org>

pkgname=dae
pkgver=1.0.0
pkgrel=1
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=('x86_64')
url="https://github.com/daeuniverse/dae"
license=('AGPL-3.0-or-later')
depends=(
    'glibc'
    'v2ray-rules-dat'
)
makedepends=(
    'clang'
    'go'
)
provides=("dae")
backup=("etc/dae/config.dae")
source=(
    "${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/${pkgname}-full-src.zip"
    "0001-clean-BUILD_ARGS.patch"
)
sha256sums=('d933b93fc30cb4e9941cbb1be23557bd6caa2a33af212883505fec435d06fc13'
            'b7ab995a9e5ae57e710b019d866c47ef7fdeb96f9b8e7a57a1edeac0fe7aad81')

prepare() {
    patch -d "${srcdir}/" -Np1 < "0001-clean-BUILD_ARGS.patch"
}

build() {
    export CFLAGS="-fno-stack-protector"
    export CGO_ENABLED=1
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export BUILD_ARGS='-buildmode=pie \
                        -trimpath \
                        -ldflags=-linkmode=external \
                        -mod=readonly \
                        -modcacherw \
                        -ldflags "-s -w -X github.com/daeuniverse/dae/cmd.Version=$(VERSION) \
                            -X github.com/daeuniverse/dae/common/consts.MaxMatchSetLen_=$(MAX_MATCH_SET_LEN)"'
    make VERSION="${pkgver}"
}

package() {
    install -Dvm755 "${srcdir}/dae" "${pkgdir}/usr/bin/dae"
    install -Dvm644 "${srcdir}/install/dae.service" -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dvm600 "${srcdir}/example.dae" "${pkgdir}/etc/dae/config.dae"

    mkdir -pv "${pkgdir}/usr/share/${pkgname}"
    ln -sv ../v2ray-rules-dat/{geoip.dat,geosite.dat} "${pkgdir}/usr/share/dae"

    install -Dvm644 "${srcdir}/install/shell-completion/dae.bash" "${pkgdir}/usr/share/bash-completion/completions/dae"
    install -Dvm644 "${srcdir}/install/shell-completion/dae.zsh" "${pkgdir}/usr/share/zsh/site-functions/_dae"
    install -Dvm644 "${srcdir}/install/shell-completion/dae.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/dae.fish"
}
