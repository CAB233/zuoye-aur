# Maintainer: yidaduizuoye <yidaduizuoye at outlook dot com>

pkgname=sing-box
pkgver=1.12.13
pkgrel=2
pkgdesc='The universal proxy platform.'
arch=('x86_64')
url='https://sing-box.sagernet.org/'
license=('custom:GPL-3.0-or-later WITH name use or association addition')
depends=(
    'glibc'
    'dbus'
    'polkit'
)
makedepends=(
    'git'
    'go'
)
source=("git+https://github.com/SagerNet/sing-box.git#tag=v${pkgver}")
sha256sums=('28c2343235aeca576210a34b79f5ab30c5c078556f87614e15ad08a20d3fc6e4')
provides=("${pkgname}")
conflicts=("${pkgname}")
backup=("etc/${pkgname}/config.json")

build() {
    local tags=(
        'with_quic'
        #'with_grpc'
        'with_dhcp'
        'with_wireguard'
        'with_utls'
        'with_acme'
        'with_clash_api'
        #'with_v2ray_api'
        'with_gvisor'
        'with_tailscale'
    )
    local ldflags=(
        "-s"
        "-w"
        "-linkmode=external"
        "-X github.com/sagernet/sing-box/constant.Version=${pkgver}"
    )

    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1

    cd "${pkgname}"
    go build \
        -v \
        -tags "${tags[*]}" \
        -ldflags "${ldflags[*]}" \
        ./cmd/sing-box

    install -d completions
    go run ./cmd/sing-box completion bash > completions/bash
    go run ./cmd/sing-box completion fish > completions/fish
    go run ./cmd/sing-box completion zsh > completions/zsh
}

package() {
    cd "${pkgname}"
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm644 "release/config/config.json" -t "${pkgdir}/etc/${pkgname}"
    install -Dm644 "release/config/${pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 "release/config/${pkgname}@.service" -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 "release/config/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    install -Dm644 "release/config/${pkgname}.rules" -t "${pkgdir}/usr/share/polkit-1/rules.d"
    install -Dm644 "release/config/${pkgname}-split-dns.xml" "${pkgdir}/usr/share/dbus-1/system.d/${pkgname}.conf"

    install -Dm644 completions/bash "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
    install -Dm644 completions/fish "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
    install -Dm644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
