# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Dct Mei <dctxmei@yandex.com>
# Contributor: AkinoKaede <autmaple@protonmail.com>
# Contributor: DuckSoft <realducksoft@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

_repo=Xray-core
pkgname=xray
pkgver=26.2.4
pkgrel=1
pkgdesc='The best v2ray-core, with XTLS support'
arch=('x86_64')
url='https://github.com/XTLS/Xray-core'
license=('MPL-2.0')
depends=(
    'glibc'
    'v2ray-rules-dat'
)
makedepends=(
    'go'
    'git'
)
provides=('v2raya-core')
source=(
    "git+${url}.git#tag=v${pkgver}"
    "xray.sysusers"
    "xray.tmpfiles"
    "xray.service"
    "xray@.service"
)
sha256sums=('fe58ac5e9e2f96726eac60cce1101ae3d85abd07581674511a4df103096f79dd'
            '801131bf2eb079750f17d3e703e414eab8494db0d512164cdef3cc68cef308b8'
            '2d301e9f2fae728da55f33a15b2c36e90cdb657deafb5d6ab7d74375ce9fdf38'
            '03947eec80d413a0581f6fccbe204ee3b701887b9fda218560793263f2c90516'
            '0fd45a725c21a07bfee64440360794b4d90827823f7c6f25c1f2be6099a57e44')

prepare() {
    cd "${_repo}"
    commit=$(git describe --always --dirty)
}

build() {
    cd "${_repo}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1
    go build \
        -v \
        -ldflags " \
            -s -w \
            -linkmode=external \
            -X 'github.com/xtls/xray-core/core.build=${commit}'" \
        -o xray \
        ./main
}

package() {
    cd "${_repo}"
    install -d "${pkgdir}"/etc/xray/
    install -Dm 755 xray -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/xray/
    install -Dm 644 "${srcdir}"/xray.sysusers "${pkgdir}"/usr/lib/sysusers.d/xray.conf
    install -Dm 644 "${srcdir}"/xray.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/xray.conf
    install -Dm 644 "${srcdir}"/xray.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/xray@.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -d "${pkgdir}/usr/share/xray"
    ln -sv ../v2ray-rules-dat/{geoip.dat,geosite.dat} "${pkgdir}/usr/share/xray"
}
