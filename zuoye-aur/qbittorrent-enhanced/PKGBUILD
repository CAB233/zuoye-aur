# Maintainer: Leo <i@setuid0.dev>
pkgname=qbittorrent-enhanced
pkgver=5.1.3.10
pkgrel=2
pkgdesc="A bittorrent client powered by C++, Qt and libtorrent (Enhanced Edition)"
arch=('x86_64')
url="https://github.com/c0re100/qBittorrent-Enhanced-Edition"
license=('GPL-2.0-or-later')
depends=(
    'gcc-libs'
    'glibc'
    'libtorrent-rasterbar'
    'openssl'
    'qt6-base'
    'qt6-svg'
    'hicolor-icon-theme'
    'zlib'
)
makedepends=(
    'cmake'
    'boost>=1.65'
    'qt6-tools'
    'ninja'
)
optdepends=('python: needed for torrent search tab')
provides=('qbittorrent')
conflicts=('qbittorrent')
source=("qbittorrent-enhanced-${pkgver}.tar.gz::https://github.com/c0re100/qBittorrent-Enhanced-Edition/archive/release-${pkgver}.tar.gz")
sha256sums=('b153428db354775de59b850751ad9d6bafb418dde115b3208b41eab14308f6d1')

build() {
    cd qBittorrent-Enhanced-Edition-release-${pkgver}
    cmake -G Ninja -B build -S . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd qBittorrent-Enhanced-Edition-release-${pkgver}
    DESTDIR="${pkgdir}" cmake --install build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
