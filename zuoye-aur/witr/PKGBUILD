# Maintainer: yidaduizuoye <yidaduizuoye at outlook dot com>

pkgname=witr
pkgver=0.3.0
pkgrel=1
pkgdesc='CLI tool that explains the causal chain behind running processes'
arch=('x86_64')
url='https://github.com/pranshuparmar/witr'
license=('Apache-2.0')
depends=('glibc')
makedepends=(
    'git'
    'go'
)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('b4106952853c712bf60d0e9871d2eeecec8045d171360faec9e17329dbbe2653')

build() {
    cd "$pkgname"
    local COMMIT=$(git rev-parse --short HEAD)
    local DATE=$(date +%Y-%m-%d)
    local ldflags=(
        "-s"
        "-w"
        "-linkmode=external"
        "-X main.version=$pkgver"
        "-X main.commit=$COMMIT"
        "-X main.buildDate=$DATE"
    )
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1

    go build \
        -v \
        -tags "${tags[*]}" \
        -ldflags "${ldflags[*]}" \
        ./cmd/witr
}

package() {
    cd "$pkgname"
    install -Dvm755 "$pkgname" -t "$pkgdir/usr/bin/"
    install -Dvm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
    install -Dvm644 docs/cli/witr.1 "$pkgdir/usr/share/man/man1/witr.1"
}
