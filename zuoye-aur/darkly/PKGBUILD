# Maintainer: DeltaCopy <7x0bb03yq@mozmail.com>

pkgname="darkly"
_gitname="Darkly"
pkgver=0.5.30
pkgrel=1
pkgdesc="Fork of Lightly (A modern style for Qt applications)"
url="https://github.com/Bali10050/darkly"
arch=('x86_64' 'aarch64')
license=("GPL-2.0-or-later")
depends=(
    'glibc'
    'gcc-libs'
    'hicolor-icon-theme'
    'kdecoration'
    'kcoreaddons'
    'kcmutils'
    'kcolorscheme'
    'kconfig'
    'kguiaddons'
    'ki18n'
    'kiconthemes'
    'kwidgetsaddons'
    'kwindowsystem'
    'frameworkintegration'
    'qt6-base'
    'qt6-declarative'
)
makedepends=(
    'cmake'
    'extra-cmake-modules'
    'ninja'
)
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('4527949decb0b4715ef24a44e327f9a6189d7d0221a26fe634c5fab01530ed6a')
options=(!emptydirs)

build() (
    cmake -G Ninja -B build -S "$_gitname-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_QT5=OFF \
        -DBUILD_QT6=ON \
        -DBUILD_TESTING=OFF
    cmake --build build
)

package() (
    DESTDIR="$pkgdir" cmake --install build
)
